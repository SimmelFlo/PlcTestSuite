<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="Test" Id="{9e8aece1-ee26-4cf6-b4f4-25a27cfed9dc}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK Test IMPLEMENTS I_TestOperation
VAR
	_TestSuiteName : STRING;
	_TestId		   : T_MaxString;
	_State		   : E_TestStatus;
	TestMessage	   : FB_TcMessage;
	Source		   : FB_TcSourceInfo;
	Stopwatch	   : Stopwatch;
	ResultFile	   : I_UnitTestResultFile;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="AssertEqual" Id="{848da2d4-fc83-435a-a497-3700fbb8f173}">
      <Declaration><![CDATA[METHOD AssertEqual
VAR_INPUT
	Expected : ANY;
	Actual	 : ANY;
	Epsilon	 : LREAL := 0.000001;
END_VAR

VAR
	nCount		   : DINT;
	StringExpected : T_MaxString;
	StringActual   : T_MaxString;
	LrealExpected  : LREAL;
	LrealActual	   : LREAL;
	RealExpected   : REAL;
	RealActual	   : REAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Stopwatch.Stop();
IF Expected.typeclass <> Actual.typeclass THEN
	State := E_TestStatus.DoneFailed;
	ReportAssert('Type missmatch!');
	RETURN;
END_IF

IF Expected.diSize <> Actual.diSize THEN
	State := E_TestStatus.DoneFailed;
	ReportAssert('Type size missmatch!');
	RETURN;
END_IF

// String
IF Expected.TypeClass = __SYSTEM.TYPE_CLASS.TYPE_ANYSTRING OR Expected.TypeClass = __SYSTEM.TYPE_CLASS.TYPE_STRING OR Expected.TypeClass = __SYSTEM.TYPE_CLASS.TYPE_WSTRING OR Expected.TypeClass = __SYSTEM.TYPE_CLASS.TYPE_XSTRING THEN
	MEMCPY(destAddr := ADR(StringExpected), srcAddr := Expected.pValue, n := DINT_TO_UDINT(Expected.diSize));
	MEMCPY(destAddr := ADR(StringActual), srcAddr := Actual.pValue, n := DINT_TO_UDINT(Actual.diSize));
	IF StringExpected <> StringActual THEN
		State := E_TestStatus.DoneFailed;
		ReportAssert(CONCAT(CONCAT('Expected: ', GetValueString(Expected)), CONCAT(' , Actual: ', GetValueString(Actual))));
		RETURN;
	END_IF
ELSIF Expected.TypeClass = __SYSTEM.TYPE_CLASS.TYPE_LREAL THEN
	MEMCPY(destAddr := ADR(LrealExpected), srcAddr := Expected.pValue, n := DINT_TO_UDINT(Expected.diSize));
	MEMCPY(destAddr := ADR(LrealActual), srcAddr := Actual.pValue, n := DINT_TO_UDINT(Actual.diSize));
	IF ((LrealExpected - Epsilon) > LrealActual) OR ((LrealExpected + Epsilon) < LrealActual) THEN
		State := E_TestStatus.DoneFailed;
		ReportAssert(CONCAT(CONCAT('Expected: ', GetValueString(Expected)), CONCAT(' , Actual: ', GetValueString(Actual))));
		RETURN;
	END_IF
ELSIF Expected.TypeClass = __SYSTEM.TYPE_CLASS.TYPE_ANYREAL OR Expected.TypeClass = __SYSTEM.TYPE_CLASS.TYPE_REAL THEN
	MEMCPY(destAddr := ADR(RealExpected), srcAddr := Expected.pValue, n := DINT_TO_UDINT(Expected.diSize));
	MEMCPY(destAddr := ADR(RealActual), srcAddr := Actual.pValue, n := DINT_TO_UDINT(Actual.diSize));
	IF ((RealExpected - Epsilon) > RealActual) OR ((RealExpected + Epsilon) < RealActual) THEN
		State := E_TestStatus.DoneFailed;
		ReportAssert(CONCAT(CONCAT('Expected: ', GetValueString(Expected)), CONCAT(' , Actual: ', GetValueString(Actual))));
		RETURN;
	END_IF
ELSE
	// Byte comparison
	FOR nCount := 0 TO Actual.diSize - 1 DO
		IF Expected.pvalue[nCount] <> Actual.pvalue[nCount] THEN
			State := E_TestStatus.DoneFailed;
			ReportAssert(CONCAT(CONCAT('Expected: ', GetValueString(Expected)), CONCAT(' , Actual: ', GetValueString(Actual))));
			RETURN;
		END_IF
	END_FOR
END_IF

IF ResultFile <> 0 THEN
	ResultFile.AddTestCaseResult(_TestSuiteName, TestId, Stopwatch.Elapsed);
END_IF

State := E_TestStatus.DoneOk;]]></ST>
      </Implementation>
    </Method>
    <Property Name="Duration" Id="{45e82a09-3d77-4e43-90c0-062b39afb45f}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY Duration : LREAL]]></Declaration>
      <Get Name="Get" Id="{c3926804-129a-47ee-908c-b9d9a6725716}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[Duration := Stopwatch.Elapsed;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="ExecuteTest" Id="{d3490801-b397-4b9a-8a9d-6332802fd972}">
      <Declaration><![CDATA[METHOD ExecuteTest : BOOL
VAR_INPUT
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF State = E_TestStatus.Init THEN
	Stopwatch.Start();
	State := E_TestStatus.Running;
END_IF

ExecuteTest := State = E_TestStatus.Running;]]></ST>
      </Implementation>
    </Method>
    <Method Name="ReportAssert" Id="{70613942-8c38-478a-ad8e-0d3b3e114371}">
      <Declaration><![CDATA[METHOD PRIVATE ReportAssert
VAR_INPUT
	Reason : STRING := '';
END_VAR

VAR
	ResultString : T_MaxString;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Source.sName := _TestSuiteName;
TestMessage.Create(eventClass	:= TC_EVENT_CLASSES.TestEvents,
				   nEventId		:= 1,
				   eSeverity	:= TcEventSeverity.Error,
				   ipSourceInfo := Source);

ResultString := CONCAT('Test failed - ', TestId);
ResultString := CONCAT(ResultString, '! ');
ResultString := CONCAT(ResultString, Reason);
TestMessage.ipArguments.AddString(ResultString);
TestMessage.Send(0);
Source.Clear();
TestMessage.Release();
IF ResultFile <> 0 THEN
	ResultFile.AddTestCaseResult(_TestSuiteName,
								 TestId,
								 Stopwatch.Elapsed,
								 'Test failed!',
								 'AssertionError',
								 Reason);
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="ResultFileFunction" Id="{7b529fb8-591c-4188-b3da-fc47f7a437a2}">
      <Declaration><![CDATA[PROPERTY ResultFileFunction : I_UnitTestResultFile]]></Declaration>
      <Set Name="Set" Id="{4ccaac66-e459-4c6e-85c3-92277f478409}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[ResultFile := ResultFileFunction;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="State" Id="{107dda4b-11bf-4131-8c5f-64011be55284}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY State :  E_TestStatus]]></Declaration>
      <Get Name="Get" Id="{262d28c9-71be-4402-bd2c-a86901ea6f86}">
        <Declaration><![CDATA[VAR
END_VAR]]></Declaration>
        <Implementation>
          <ST><![CDATA[State := _State;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{71ba93ec-d3d4-4f07-892c-f28b00d83612}">
        <Declaration><![CDATA[VAR
END_VAR]]></Declaration>
        <Implementation>
          <ST><![CDATA[_State := State;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="TestId" Id="{0723af9d-7a0b-479f-bfbc-daf60795c791}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY TestId : T_MaxString]]></Declaration>
      <Get Name="Get" Id="{b9dea5fd-624e-4410-bf21-8a9d9c1236f8}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[TestId := _TestId;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{b3f6ef0d-a7cd-4254-a831-4165b3f53021}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_TestId := TestId;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="TestSuiteName" Id="{d7b0c5f4-02c6-4dcf-854f-e251023d9405}">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY TestSuiteName : STRING]]></Declaration>
      <Set Name="Set" Id="{a224d3ab-daa7-49af-bafa-8e932753e41c}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_TestSuiteName := TestSuiteName;]]></ST>
        </Implementation>
      </Set>
    </Property>
  </POU>
</TcPlcObject>